dnl Process this file with autoconf to produce a configure script.

AC_INIT(configure.in)
AM_INIT_AUTOMAKE(liferea, 0.4.7)
AM_CONFIG_HEADER(config.h)
AM_MAINTAINER_MODE

AC_ISC_POSIX
AC_PROG_CC
AM_PROG_CC_STDC
AC_HEADER_STDC
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET
AM_PROG_LIBTOOL
libdir='${prefix}/lib/liferea'

html_modules=

AC_MSG_CHECKING([for GtkHTML support])
temptest=`pkg-config --cflags libgtkhtml-2.0 2> /dev/null`
AM_CONDITIONAL(WITH_GTKHTML, test "x$temptest" != "x")
if test "x$temptest" != "x" ; then
   AC_MSG_RESULT(yes)
   PKG_CHECK_MODULES(GTKHTML, libgtkhtml-2.0 >= 2.0.0 gnome-vfs-2.0 >= 2.0.0)
   AC_SUBST(GTKHTML_CFLAGS)
   AC_SUBST(GTKHTML_LIBS)
else
   AC_MSG_RESULT(no)
fi

AC_MSG_CHECKING([for Mozilla support])
temptest=`pkg-config --cflags mozilla-gtkmozembed 2> /dev/null`
AM_CONDITIONAL(WITH_MOZILLA, test "x$temptest" != "x")
if test "x$temptest" != "x" ; then
   AC_MSG_RESULT(yes)
   PKG_CHECK_MODULES(MOZILLA, mozilla-gtkmozembed)
   AC_SUBST(MOZILLA_CFLAGS)
   AC_SUBST(MOZILLA_LIBS)
else
   AC_MSG_RESULT(no)
fi

# hmm... I really have no clue which versions are neccessary!
pkg_modules="	gtk+-2.0 >= 2.0.0 
		gconf-2.0 >= 2.0.0 
		libxml-2.0 >= 2.0.0 
		gmodule-2.0 >= 2.0.0"
		
# if you want to compile on RedHat you should change the
# dependencies to something like this
#
#		gtk+-2.0 >= 2.0.0 
#		gconf2 >= 2.0.0 
#		libxml-2.0 >= 2.0.0 
#		gtkhtml2 >= 2.0.0
#		gnome-vfs2 >= 2.0.0

PKG_CHECK_MODULES(PACKAGE, [$pkg_modules])
AC_SUBST(PACKAGE_CFLAGS)
AC_SUBST(PACKAGE_LIBS)

GETTEXT_PACKAGE=liferea
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE,"$GETTEXT_PACKAGE")

uname=`uname`
AC_DEFINE_UNQUOTED(OS, $uname, [defines a OS version string, used for OS specific code])
AC_DEFINE_UNQUOTED(OSNAME, "$uname", [defines a OS version string, used for the user agent string])
AC_MSG_RESULT(user agent OS = $uname)

dnl Add the languages which your application supports here.
ALL_LINGUAS="bg de es_ES fr ja ko"
AM_GLIB_GNU_GETTEXT

AC_OUTPUT([
Makefile
src/Makefile
po/Makefile.in
])

